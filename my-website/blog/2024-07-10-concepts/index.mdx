---
slug: The Magnificent Seven - Mastering KDB/Q Concepts for Data Excellence
title: The Magnificent Seven - Mastering KDB/Q Concepts for Data Excellence
authors: [alexander]
hide_table_of_contents: true
tags: [kdb/q, kdb/q concepts]
---

When developers first encounter KDB/Q, they are often intimidated by its "strange" syntax, which differs significantly from most other programming languages they've seen. However, understanding and familiarizing oneself with the syntax is merely the beginning. To truly master any programming language, one needs a deep understanding of its core concepts and paradigms. For instance, when learning object-oriented languages like Java or C++, you should focus on concepts such as inheritance, encapsulation, polymorphism, and data abstraction. Additionally, understanding pointers and memory allocation is crucial for mastering C++. The same principle applies to KDB/Q, a high-performance, in-memory database and programming language. In this blog post, I will share the seven most important concepts that will set you apart and enhance your skills as a KDB/Q developer. Understanding these concepts will provide insight into why KDB/Q is so powerful and favoured among quants and data enthusiasts.

<!-- truncate -->

:::note
Please note, this blog post is primarily aimed at readers who are new to the KDB/Q programming language or just starting their learning journey. If you already have some experience with KDB/Q or are a seasoned developer, you might already be familiar with some or all of these concepts.
:::

![The Magnificient Seven](./seven.png)

## Left of Right and no Operator Precedence

### Concept
One of the most common mistakes made by qbies (newcomers to KDB/Q) is forgetting that KDB/Q is left-of-right, meaning it's read from right to left and has no operator precedence.
### Details
Unlike traditional programming languages like Java, C++, or Python, KDB/Q reads expressions from right to left. This makes expression evaluation simple and uniform, without unnecessary parentheses cluttering the code. Left-to-right evaluation eliminates any ambiguity in expressions, at least from the compiler's perspective if not yours. While parentheses can still be used to group terms and override the default evaluation order, you will find them much less necessary once you break old habits.
### Example
When evaluating the expression `3*2+3` in KDB/Q, the result is `15`, rather than `9` as you might expect from traditional programming languages. This happens because KDB/Q adds `3` to `2` first, resulting in `5`, which is then multiplied by `3` to get the final result of `15`. To evaluate the multiplication first, you need to use parentheses around the expression. 
```
q)3*2+3
15
q)(3*2)+3
9
```

## KDB/Q is Atomic

### Concept
Another important concept of KDB/Q is that most built-in operators are atomic. This means that functions iterate recursively through data structures such as lists or dictionaries until they reach individual atoms and then apply the function to it.
### Details
Atomic functions apply recursively to every element of a list or dictionary. For unary atomic functions (those that take one parameter), the function applies to both atoms and lists, and for lists, it applies independently to each atom within the list. Binary atomic functions (those that take two parameters) can be atomic in both operands, resulting in four different applications:

- Both operands are atoms.
- The first operand is a list, and the second is an atom.
- The first operand is an atom, and the second is a list.
- Both operands are lists.
### Example
```
// unary atomic function
q)neg 10
-10
q)neg 10 20 30
-10 -20 -30
q)neg (10 20 30; 40 50)
-10 -20 -30
-40 -50
q)neg `a`b`c!10 20 30
a| -10
b| -20
c| -30
q)neg `a`b`c!(10 20; 30 40 50; 60)
a| -10 -20
b| -30 -40 -50
c| -60
// binary atmoic function
q)1+10
11
q)1+10 20 30
11 21 31
q)1 2 3+10
11 12 13
q)1 2 3+10 20 30
11 22 33
```

This atomic behavior makes KDB/Q a very powerful and efficient programming language. Imagine you have a nested list of numbers, some of which are null. Your task is to replace all null values with `0`. In a traditional programming language like Java, you would need to loop through every element of the list and then through each sublist, as a nested list can contain multiple lists. In KDB/Q, however, this task is as simple as using the [`greator OR |`](https://code.kx.com/q/ref/greater/) operator

```
q)((0N 1 2 0N);((1 3;0N 1 2);(1 2 3;0N 0N 1 2));4 5 0N)
0N 1 2 0N
((1 3;0N 1 2);(1 2 3;0N 0N 1 2))
4 5 0N
q)0|((0N 1 2 0N);((1 3;0N 1 2);(1 2 3;0N 0N 1 2));4 5 0N)
0 1 2 0
((1 3;0 1 2);(1 2 3;0 0 1 2))
4 5 0
```


## Vector Operations and Column Oriented

### Concept
The previous concept of KDB/Q being atomic makes it inherently a vector-oriented language, meaning it operates on entire arrays (vectors) of data in a single operation.
### Details
Instead of processing individual elements in loops, KDB/Q applies functions and operations to whole vectors at once. This not only simplifies code but also significantly boosts performance by leveraging CPU vectorization capabilities. 
### Example
To add two vectors `a` and `b`, you simply write `c:a+b`. This operation is applied element-wise across the entire vectors.
```
q)a:1 2 3
q)b:4 5 6
q)show c:a+b
5 7 9
```
This behaviour also extends to tables. KDB/Q is column-oriented and stores tables as flipped column dictionaries, with each column stored in contiguous memory as a vector. This setup allows for vectorized mathematical operations, making table manipulation incredibly fast.

```
q)show t:([] a:1 2 3;b:4 5 6)
a b
---
1 4
2 5
3 6
q)update c:a+b,d:a*b,e:neg a,f:10+b from t
a b c d  e  f
--------------
1 4 5 4  -1 14
2 5 7 10 -2 15
3 6 9 18 -3 16
```

## Interpreted and dynamic

### Concept
KDB/Q code is interpreted at runtime, and both variables and functions are dynamic. This means they can hold data of any type, and the type can change during execution.
### Details
KDB/Q is both interpreted and dynamic, meaning that code doesn't need to be compiled and all functions are compiled into bytecode at runtime. Additionally, as a dynamic language, function definitions and variable types can be changed at runtime, with no type checking performed before execution. While these features make KDB/Q highly flexible, they also require careful design, thorough testing, and robust error handling to prevent production outages.
### Example
Below, we illustrate the dynamic behavior of KDB/Q by first assigning a string to a variable `a`, then changing it to a numerical value, and finally to a function that we can execute.
```
q)show a:"Hello"
"Hello"
q)show a:2
2
q)show a:{x+y}
{x+y}
q)a[2;3]
5
```
## Functional

### Concept
KDB/Q heavily incorporates functional programming paradigms, emphasizing the use of functions as first-class citizens.
### Details
Functions can be passed as arguments, returned from other functions, and composed to build more complex functionality. KDB/Q's syntax and functions make it easy to perform operations like map, reduce, and filter directly on data sets. However, it's worth mentioning that KDB/Q is not purely functional as functions can have side effects.
### Example
Applying a function to each element of a list can be done with `f each list`, where `f` is a user-defined function.
```
q)f:count
q)count each ("Hello";1 2 3;(2 3;4 5))
5 3 2
```

## Dictionaries, Tables and Keyed Tables

### Concept
KDB/Q provides powerful data structures for efficient data storage and manipulation natively and supports both row-oriented and column-oriented data access. These data structures include dictionaries, tables and keyed tables.
### Details
Dictionaries, Tables and Keyed Tables are data structures designed for high performance, efficient data storage and access and ideal for analytical tasks. All three data structures are supported by KDB/Q out of the box and are optimised to achieve optimal performance. In KDB/Q, Tables are similar to SQL tables with the difference that KDB/Q is column oriented, meaning each column is stored as a vector in contiguous memory, allowing for vectorised operations and making mathematical computations lightning fast. 
### Example
Demonstrating the power and performance of tables in analytics
```
// First we create a dummy table with random data
q)show t:([] sym:10?`AAPL`C`C`MSFT`AAPL; price:10?100.0; qty:10?1000)
sym  price    qty
-----------------
C    49.31835 908
C    57.85203 360
AAPL 8.388858 522
C    19.59907 257
C    37.5638  858
C    61.37452 585
C    52.94808 90
AAPL 69.16099 683
AAPL 22.96615 90
C    69.19531 869
// Calculating the volume-weighted average price (VWAP) per symbol for the data
q)select wvap:price wavg qty by sym from t
sym | wvap
----| --------
AAPL| 534.0731
C   | 585.5224
// Updating the table with the VWAP
q)update wvap:price wavg qty by sym from t
sym  price    qty wvap
--------------------------
C    49.31835 908 585.5224
C    57.85203 360 585.5224
AAPL 8.388858 522 534.0731
C    19.59907 257 585.5224
C    37.5638  858 585.5224
C    61.37452 585 585.5224
C    52.94808 90  585.5224
AAPL 69.16099 683 534.0731
AAPL 22.96615 90  534.0731
C    69.19531 869 585.5224
// Retrieving the well-known metrics: open, high, low, and close for each symbol
q)exec `o`h`l`c!(first;max;min;last)@\:price by sym from t
    | o        h        l        c
----| -----------------------------------
AAPL| 8.388858 69.16099 8.388858 22.96615
C   | 49.31835 69.19531 19.59907 69.19531
```
## Synchronous and Asynchronous IPC for Real-Time Data

### Concept
No other programming language makes interprocess communication (IPC) as easy and straightforward as KDB/Q. Designed for real-time data processing and streaming, KDB/Q supports both synchronous and asynchronous messaging right out of the box.
### Details
Designing event driven applications is critical for financial applications where real-time data feeds and low-latency processing are essential. KDB/Q processes and updates data in real-time, handling high-throughput data streams efficiently. Opening connections between two KDB/Q processes, whether on the same server or different servers, requires minimal effort and can be accomplished with a single command. Data is serialized before being sent and deserialized upon receipt, minimizing the amount of data transferred over the network. Asynchronous communication and advanced IPC patterns can be leveraged to design efficient and scalable applications.
### Example
```
// Server: start a q process and assign port 5001 to it
q)\p 5001
q)
// Client: Open a connection to the server using hopen and send a synchronous query to it
q)h:hopen `::5001
q)h"2+2"
4
```

## Bonus: Single Threaded

### Concept
KDB/Q operates in a single-threaded mode by default, ensuring data consistency and avoiding race conditions, as all operations are executed in the order they are received.
### Details
Even though KDB/Q is single-threaded, simplifying program design and avoiding the complexities of multi-threaded applications, you can still leverage parallel processing by starting your KDB/Q process with secondary threads using `-s n`, where `n` is the number of secondary threads. Moreover, since KDB/Q version 4.0, all primitive operators are implicitly multi-threaded. Starting your KDB/Q process with a negative secondary threads flag `-s -n` will utilize parallel processes instead of secondary threads for parallel processing.
### Example
```
q)f:{sum exp x?1.0}
q)\ts f each 2#1000000
25 16777728
q)\ts f peach 2#1000000 ~
0 1120
```

## Conclusion
Understanding these key concepts is crucial for mastering KDB/Q and elevating your development skills. Each concept, from the left-of-right evaluation and atomic functions to dynamic typing and interprocess communication, contributes to the power and efficiency of KDB/Q, making it a preferred choice for real-time data processing and high-performance analytics. By familiarizing yourself with these principles, you'll be better equipped to leverage the full potential of KDB/Q, enabling you to write more efficient, robust, and maintainable code. 

If you like to deepen your knowledge further, check out the following references and links

- [How To Read, Understand and Learn KDB/Q Code](https://www.defconq.tech/blog/How%20to%20Read,%20Understand%20and%20Learn%20KDB/Q%20code)
- [Dictionaries and Tables](https://www.defconq.tech/docs/concepts/dictionariesTables)
- [The Plain Vanilla Tick Setup](https://www.defconq.tech/docs/architecture/plain)
- [KDB Tick Explained - A Walkthrough](https://www.defconq.tech/docs/tutorials/tick)
- [Unleashing the Power of Time-Series Analysis and Big Data: Why KDB/Q Reigns Supreme](https://www.defconq.tech/docs/language/why_KDB)

